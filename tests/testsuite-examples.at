KAT_BANNER([Checking in-tree compilation of provided examples],
  [],[examples],[examples],[examples])

m4_define([KAT_EX_FAILING],[branchbound_kaapi++])
m4_define([KAT_EX_noWerror],[matprod_rec_kaapi++])

m4_define([_KAT_CHECK_BUILD_HOOK],[
  AT_SKIP_IF([AS_CASE([" $KAT_EX_CURRENT_LIST "],[*" $1 "*],[false],[:])])
  AT_XFAIL_IF([AS_CASE([" KAT_EX_FAILING "],[*" $1 "*],[:],[false])])
])

dnl KAT_EX_CHECK_BUILD(name, extra build cmd)
m4_define([KAT_EX_CHECK_BUILD],[
  AS_CASE([" KAT_EX_noWerror "],[*" $1 "*],[
    KAT_CHECK_BUILD([$1])
  ],[
    KAT_CHECK_BUILD([$1],[CPPFLAGS=-Werror])
  ])
])

dnl KAT_EX_CHECK_PROG(prog, subdir, keywords)
m4_define([KAT_EX_CHECK_PROG],[
  KAT_SETUP([$2 / $1],[$2 $1 $3], [$1])
  KAT_EX_CHECK_BUILD([$1])
  AT_CLEANUP
])

dnl ******************************************************

m4_include([testsuite-examples-inc.at])
